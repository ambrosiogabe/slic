#==========================================================================
# File--------sort.slic
# Programmer--Bryan Crawley
# Project-----Selection Sort
#
# This program performs a Selection Sort on a list of 10 integers.
# The list is held in a one-dimensional array.
#==========================================================================

main;
	data:
		integer: Temp, Top, J, List[10];
		integer: MaxLoc, Size;

	algorithm:
		Size := 10;

		# Read in the list
		counting J upward 0 to Size-1;
			print "Element #", J, ": ";
			read Temp;
			List[J] := Temp;
		end counting;

		# Perform the selection sort
		print !;
		counting Top downward Size-1 to 1;
			MaxLoc := 0;
			counting J upward 1 to Top;
				if (List[J] > List[MaxLoc]);
					MaxLoc := J;
				end if;
			end counting;
			Temp := List[Top];
			List[Top] := List[MaxLoc];
			List[MaxLoc] := Temp;
		end counting;

		# Display the sorted list
		counting J upward 0 to Size-1;
			print "Sorted #",J,": ",List[J],!;
		end counting;

		print !;
		exit;
end main;
