#=================================================================
# File--------sort.slic
# Programmer--Bryan Crawley
# Project-----Slic example
# 
# This program uses a Selection Sort to sort a list of ten
# integers into ascending order.
#=================================================================

main;
	data:
		integer: List[10];  # The list of integers.
		integer: Size; # Number of entries in the list.
		integer: Top, J;    # Counters for counting loop.
		integer: MaxLoc;    # Index of largest value in list.
		integer: Temp;      # Used in exchanging array values.
		
	algorithm:
		Size := 10; 
		
		# Enter and print the initial list of numbers.
		counting J upward 0 to Size-1;
			print "Element #", J, ": ";
			read List[J];
		end counting;
		
		print !;
		
		# Sort the numbers with a Selection Sort.
		counting Top downward Size-1 to 1;
			MaxLoc := 0;
			counting J upward 1 to Top;
				if List[J] > List[MaxLoc];
					MaxLoc := J;
				end if;
			end counting;
			Temp := List[Top];
			List[Top] := List[MaxLoc];
			List[MaxLoc] := Temp;
		end counting;
		
		# Print the sorted list.
		counting J upward 0 to Size-1;
			print "Sorted #",J,": ",List[J],!;
		end counting;
		print !,"Finished",!;
		exit;
end main;